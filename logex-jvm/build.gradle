
apply plugin: 'artifact-info-plugin'
apply plugin: 'kotlin-platform-jvm'

archivesBaseName = 'logex-jvm'

sourceCompatibility = 1.8

buildscript {
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }

    repositories {
        jcenter()
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    expectedBy project(":logex-common")
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

apply plugin: 'org.jetbrains.dokka'

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task run(dependsOn: classes, type: JavaExec) {
    if(project.hasProperty('myargs')){
        args(myargs.split(','))
    }
    main = 'org.web25.felix.logicexpreval.LogicExpressionEvaluatorKt'
    classpath = sourceSets.main.runtimeClasspath
    ignoreExitValue(true)
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title':'Logic Expression Evaluatori',
                'Implementation-Version':version,
                'Main-Class': 'org.web25.felix.logicexpreval.LogicExpressionEvaluatorKt'
    }
    baseName = project.name
    from { configurations.compile.collect {it.isDirectory() ? it: zipTree(it)}}
    with jar
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.kotlin
}

artifacts {
    archives sourcesJar
}

dokka {
    outputFormat 'html'
    outputDirectory "$buildDir/javadoc"
}

artifactInfo {
    packageName 'org.web25.felix.logicexpreval'
}

sourceSets {
    main.kotlin.srcDirs += "$buildDir/generated"
}